#version 450 core

#define PI (3.1415926535897932384626433832795)
#define TWO_PI (6.283185307179586476925286766559)
#define RED (vec3(1, 0, 0))
#define GREEN (vec3(0, 1, 0))
#define BLUE (vec3(0, 0, 1))
#define BLACK (vec3(0, 0, 0))

layout(local_size_x = 32, local_size_y = 32, local_size_z = 1) in;

const int MAX_SAMPLES = 185;
const float EPSILSON = 0.000001; 
const vec2 fragCoord = vec2(gl_GlobalInvocationID.xy)/(vec2(gl_WorkGroupSize.xy * gl_NumWorkGroups.xy) - vec2(1));


struct Box{
	vec3 min;
	vec3 max;
};

struct Weather{
	float cloud_coverage;
	float cloud_type;
	float precipitation;
	vec3 wind_direciton;
	float cloud_speed;
};

struct Ray {
	vec3 origin;
	vec3 direction;
	float tMax;
};

struct Atmosphere{
	float innerRadius;
	float outerRadius;
};

layout(rgba32f, binding=0) uniform image2D image;

layout(binding = 1) uniform sampler2D canvas;
layout(binding = 2) uniform sampler2D depthBuffer;

layout(binding = 3) uniform sampler3D cloudNoiseLowFreq;
layout(binding = 4) uniform sampler3D cloudNoiseHighFreq;
layout(binding = 5) uniform sampler2D weatherData;

layout(std430, binding=1) buffer RAY_SSB0{
	Ray rays[];
};

layout(std430, binding=2) buffer SAMPLE_POINT{
	vec4 sample_point[];
};

uniform Weather weather;
uniform Atmosphere atmosphere;
uniform mat4 MVP;
const float _4_PI = 12.566370614359172953850573533118; 
uniform vec3 camPos;
uniform vec3 stepSize;
uniform float dt;
const vec3 texMin = vec3(0);
const vec3 texMax = vec3(1);
uniform vec3 bMin;
uniform vec3 bMax;
uniform vec2 cloudMinMax;
uniform float eccentricity = 0.2;
uniform vec3 lightPos = vec3(20);

bool intersectPlane(Ray ray, vec3 n, float d, out float t, out vec2 uv);

bool intersectCube(Ray ray, Box box, out vec2 uv, out vec3 p0, out vec3 p1, out float tn, out float tr);

vec3 Checkerboard(vec2 uv);

void orthonormalBasis(vec3 n, out vec3 x, out vec3 y);

int globalIndex();

bool intersectSphere(Ray ray, float radius, out vec3 p);

float depthValue(vec3 p);

vec4 matchCloud(vec3 origin, vec3 direction);

void main(){
	int index = globalIndex();
	Ray ray = rays[index];


	bool aHit = false;
	vec2 uv;
	float t = ray.tMax;
	float t0;
	vec4 color = vec4(0);
	color.a = 1;


	Box box = Box(bMin, bMax);

	vec3 p0, p1;
	float t1;
	if(intersectCube(ray, box, uv, p0, p1, t0, t1)){
		aHit = true;
		if(t0 < t){
			t = t0;
			color.rgb = Checkerboard(uv);
		}
	}
	t0 = t0 < 0 ? t1/10 : t0;

	vec3 p = ray.origin + ray.direction * t0;
	float depthSrc = depthValue(p);
	float depthDest = texture(depthBuffer, fragCoord).r;
	if(aHit && depthSrc < depthDest){
		vec3 dest = texture(canvas, fragCoord).rgb;
		color = matchCloud(p, ray.direction);
		color.rgb = color.rgb + dest * (1 - color.a);
	}else{
		sample_point[globalIndex()] = vec4(0);
		color.rgb = texture(canvas, fragCoord).rgb;
	}

	imageStore(image, ivec2(gl_GlobalInvocationID.xy), color);
}

int globalIndex(){
	ivec3 size = ivec3(gl_WorkGroupSize * gl_NumWorkGroups);
	ivec3 pos = ivec3(gl_GlobalInvocationID);

	return pos.z * (size.y * size.x) + (pos.y * size.x) + pos.x;
}

bool pointInBox(Box box, vec3 p){
	return dot(sign(p - box.min), sign(box.max -p)) == 3;
}


bool intersectCube(Ray ray, Box box, out vec2 uv, out vec3 p0, out vec3 p1, out float tn, out float tf) {	
	
	bvec3 rayParallelToSlab = lessThan(abs(ray.direction), vec3(EPSILSON));

	if(any(rayParallelToSlab) && !pointInBox(box, ray.origin)) return false;


	// TODO check what happens when min/max applied to Inf
	vec3   tMin = (box.min - ray.origin) / ray.direction;
	vec3   tMax = (box.max - ray.origin) / ray.direction;
	vec3     t1 = min(tMin, tMax);
	vec3     t2 = max(tMin, tMax);
	tn = max(max(t1.x, t1.y), t1.z);
	tf = min(min(t2.x, t2.y), t2.z);

	if (tn > ray.tMax || tf < 0) return false;

	p0 = ray.origin + tn * ray.direction;
	p1 = ray.origin + tf * ray.direction;
	vec3 n = -sign(ray.direction) * step(t1.yzx, t1.xyz) * step(t1.zxy, t1.xyz);
	vec3 x, y;
	n = tn < 0 ? -n : n;
	orthonormalBasis(n, x, y);
	
	vec3 p = tn < 0 ? p1 : p0;
	uv = vec2(dot(p, x)/5, dot(p, y)/5);
	uv = abs(uv);
	return tn < tf;
}

bool intersectPlane(Ray ray, vec3 n, float d, out float t, out vec2 uv) {

	t = d - dot(n, ray.origin);
	t /= dot(n, ray.direction);

	if (t < 0 || t > ray.tMax) {
		return false;
	}

	vec3 p = ray.origin + t * ray.direction;

	vec3 x, y;
	orthonormalBasis(n, x, y);
		
	//	interact.uv = vec2(p.x / whole, p.z / whole);
	uv = vec2(dot(p, x)/10, dot(p, y)/10);

	return true;
}


bool intersectSphere(Ray ray, float radius, out vec3 p){
	vec3 cs = vec3(0, 0, 0);
	vec3 m = ray.origin - cs;
	float b = dot(m, ray.direction);
	float c = dot(m , m) - radius * radius;

	// ray is facing away from sphere
	if(c > 0 && b > 0) return false;

	float discr = b * b - c;

	// sqrt(-discr) imaginary number corresponds to ray missing sphere
	if(discr < 0) return false; 

	float t = 0;
	t = -b - sqrt(discr);
	
	p = ray.origin + ray.direction * t;

	return true;
}


void orthonormalBasis(vec3 n, out vec3 x, out vec3 y) {
	float s = n.z >= 0.0 ? 1.0 : -1.0;
	float a = -1.0 / (s + n.z);
	float b = n.x * n.y * a;
	x = vec3(1.0 + s * n.x * n.x * a, s * b, -s * n.x);
	y = vec3(b, s + n.y * n.y * a, -n.y);
}

vec3 Checkerboard(vec2 uv)
{
	return vec3(mod(floor(uv.x * 4.0) + floor(uv.y * 4.0), 2.0) < 1.0 ? 1.0 : 0.4);
	//return vec3(1, 0, 0);
}

float depthValue(vec3 p){
	vec4 pos = MVP * vec4(p, 1);

	if(pos.w == 0) return 1;

	// apply perspective division
	float d = pos.z/pos.w;

	// outside NDC [-1, 1]
	if(d < -1 || d > 1) return 1;

	// remap from NDC [-1, 1] to screen space [0, 1]
	return d * 0.5 + 0.5;
}

#pragma include("cloud.glsl")