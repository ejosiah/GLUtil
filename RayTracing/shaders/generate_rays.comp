#version 450 core

layout(local_size_x= 32, local_size_y = 32) in;

uniform float currentSample;

#pragma include("ray_tracing_model.glsl")

void generateRay(in CameraSample csample, in Camera camera, inout Ray ray) {
	vec3 p = (camera.rasterToCamera * vec4(csample.pFilm, 0, 1)).xyz;
	vec4 origin = vec4(vec3(0), 1);
	vec3 direction = normalize(p);

	ray.origin = (camera.cameraToWorld * origin).xyz;
	direction = mat3(camera.cameraToWorld) * direction;
	ray.direction = normalize(direction);
	ray.tMax = 1000;
}

struct Ray0 {
	vec3 origin;
	vec3 direction;
	float tMax;
	vec2 uv;
};


layout(std430, binding=0) buffer CAMERA_SSB0{
	Camera camera;
};

layout(std430, binding=1) buffer RAY_SSB0{
	Ray rays[];
};

layout(std430, binding=12) buffer RAY0_SSB0{
	Ray0 rays0[];
};

int globalIndex(){
	ivec3 size = ivec3(gl_WorkGroupSize * gl_NumWorkGroups);
	ivec3 pos = ivec3(gl_GlobalInvocationID);

	return pos.z * (size.y * size.x) + (pos.y * size.x) + pos.x;

}

vec2 hash21(float p)
{
    vec3 p3 = fract(vec3(p) * vec3(.1031, .1030, .0973));
    p3 += dot(p3, p3.yzx + 33.33);
    return fract((p3.xx + p3.yz) * p3.zy);

}

void main(){
	ivec2 pos = ivec2(gl_GlobalInvocationID.xy);
	CameraSample csample;

	float i = currentSample + 1;
	float x = mod(i - 1, 4.0);
	float y = mod(i/4, 4.0);
	vec2 jitter = hash21(i)/4;
	jitter += vec2(x, y)/4;

	csample.pFilm = vec2(pos + jitter);
	csample.time = 1.0;

	Ray ray;
	generateRay(csample, camera, ray);
	int index = globalIndex();

	Ray0 _ray = Ray0(ray.origin, ray.direction, 1000, vec2(5, 6));
	rays[index] = ray;
	rays0[index] = _ray;
}